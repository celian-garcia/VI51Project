/**
 * 
 */
package fr.utbm.info.vi51.marines.^agent

import fr.utbm.info.vi51.framework.^agent.PhysicEnvironment
import fr.utbm.info.vi51.framework.environment.PerceptionEvent
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Initialize
import io.sarl.core.Lifecycle
import java.util.ArrayList
import fr.utbm.info.vi51.framework.environment.SimulationAgentReady
import fr.utbm.info.vi51.marines.^agent.AbstractCommander
import fr.utbm.info.vi51.framework.math.Vector2f

/**
 * @author celian
 *
 */

agent Master extends AbstractCommander {
	
	uses PhysicEnvironment, Lifecycle, DefaultContextInteractions
	
	
	on Initialize {
		super._handle_Initialize_0(occurrence)
		
		// There is only one master formation
		var list = new ArrayList<Object>();
		list.add(occurrence.parameters.get(3)) 
		this.initializeOwnMasterFormation(list)
		
		// Search for a "without leader" formation
		var start = 4;
		var end = occurrence.parameters.length
		this.initializeOwnFormation(occurrence.parameters.subList(start, end))
		
		emit(new SimulationAgentReady)
		
	}

	
	on PerceptionEvent [ occurrence.perceptions.empty ] {
		
		this.updateOwnMasterFormation(occurrence.formations)
		
		// Adopt a Leader behaviour
		this.ownFormation.globalPosition = occurrence.body.position
		this.ownFormation.globalOrientation = occurrence.body.direction	
			
		// But also a Master behavior
		this.ownMasterFormation.globalPosition = occurrence.body.position
		this.ownMasterFormation.globalOrientation = occurrence.body.direction
		
		this.wanderBehaviour.runWander(
			occurrence.body.position,
			occurrence.body.direction,
			occurrence.body.currentLinearSpeed,
			occurrence.body.maxLinear,
			occurrence.body.currentAngularSpeed,
			occurrence.body.maxAngular
		).emitInfluence
	}
	
	
	on PerceptionEvent [ !occurrence.perceptions.empty ] {
		
		this.updateOwnMasterFormation(occurrence.formations)
		
		// Adopt a Leader behaviour
		this.ownFormation.globalPosition = occurrence.body.position
		this.ownFormation.globalOrientation = occurrence.body.direction	
			
		// But also a MasterChief behaviour
		this.ownMasterFormation.globalPosition = occurrence.body.position
		this.ownMasterFormation.globalOrientation = occurrence.body.direction
		
		var firstPercept = occurrence.perceptions.get(0)

		// Follow the target
		if (firstPercept.type == "TARGET") {
			var target = firstPercept.position.clone

			// run the seek behaviour
			var bo1 = this.seekBehaviour.runSeek(
				occurrence.body.position,
				occurrence.body.currentLinearSpeed,
				occurrence.body.maxLinear,
				target
			)
			
			var v = bo1.linear
			v.normalize

			for (obj:occurrence.perceptions) {
				if (obj.type == "ROCK") {

					var dmin = computeDistanceMin(
						obj.position, obj.shape.bounds,
						occurrence.body.position,
						occurrence.body.shape.bounds
					)

					var rv = obj.position.repulsiveVector(occurrence.body.position, dmin)
					v.add(rv)
				}
			}

			v.normalize
			v.scale(occurrence.body.maxLinear)
			bo1.linear = v
			
			var alignTarget = new Vector2f();
			alignTarget.sub(occurrence.body.position, target);

			bo1.angular = this.alignBehaviour.runAlign(
				occurrence.body.direction,
				occurrence.body.currentAngularSpeed,
				occurrence.body.maxAngular,
				alignTarget
			)
			bo1.emitInfluence
		}
		else {
			// run the wander behaviour
			this.wanderBehaviour.runWander(
				occurrence.body.position,
				occurrence.body.direction,
				occurrence.body.currentLinearSpeed,
				occurrence.body.maxLinear,
				occurrence.body.currentAngularSpeed,
				occurrence.body.maxAngular
			).emitInfluence
		}
	}
	
}